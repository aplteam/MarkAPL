 R←Test_FnsCall_020(stopFlag batchFlag);⎕TRAP;md;ns;expected;parms
⍝ Test function call to a niladic  public class method; must result in a bug report.
 ⎕TRAP←(999 'C' '. ⍝ Deliberate error')(0 'N')
 R←T._Failed

 #.⎕SHADOW'TEMP'
 'TEMP'#.⎕NS''
 #.TEMP.⎕FIX':Class Foo' '∇r←MyFns' ':Access Public Shared' 'r←''This comes from an "APL" function call''' '∇' ':EndClass'

 md←''
 md,←⊂'This is a para. This is a para. This is a para.'
 md,←⊂''
 md,←⊂'⍎⍎#.TEMP.Foo.MyFns⍎⍎'

 parms←MarkAPL.CreateParms
 parms.verbose←0
 ns←MarkAPL.Init parms md
 ns←MarkAPL.Process ns

 expected←'External function <#.TEMP.Foo.MyFns> is niladic but must not!'
 →T.PassesIf expected≡∊ns.report

 R←T._OK
