 R←Test_TOC_005(stopFlag batchFlag);⎕TRAP;md;ns;parms;toc;tags;expected
⍝ Test TOC generation with a header that carries inline APL code
 ⎕TRAP←(999 'C' '. ⍝ Deliberate error')(0 'N')
 R←T._Failed

 md←''
 md,←⊂'# Main `{+/⍵}`'
 md,←⊂''
 md,←⊂'Ordinary paragraph'

 parms←MarkAPL.CreateParms
 parms.toc←⍳6
 parms.verbose←0

 ns←MarkAPL.Init parms md
 ns←MarkAPL.Process ns

 toc←'<nav.*>.*</nav>'⎕R'&'⍠('Greedy' 0)('DotAll' 1)('Mode' 'M')⊣ns.html
 →T.PassesIf 1=+/∊'<ul'∘⍷¨toc
 →T.PassesIf 1=+/∊'</ul>'∘⍷¨toc
 tags←{⍵↑⍨¯1+⌊/⍵⍳'> '}¨toc
 expected←'<nav' '<ul' '<li' '</ul' '</nav'
 →T.PassesIf∧/expected∊tags
 →T.PassesIf 1=+/'<li class="toc-entry toc-h1"><a href="#main">Main <code>{+/⍵}</code></a>'⍷∊ns.html

 R←T._OK
