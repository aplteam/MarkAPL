 R←Test_TOC_004(stopFlag batchFlag);⎕TRAP;md;ns;parms;toc;tags;expected
⍝ Test TOC generation with level change from 4 to 2 with 1 suppressed. Was once a bug.
 ⎕TRAP←(999 'C' '. ⍝ Deliberate error')(0 'N')
 R←T._Failed

 md←''
 md,←⊂'# Main'
 md,←⊂'## Level 2A'
 md,←⊂'### L2A-A'
 md,←⊂'#### L2A-A-A'
 md,←⊂'#### L2A-A-B'
 md,←⊂'## Level 2B'
 md,←⊂'## Level 2C'

 parms←##.MarkAPL.CreateParms
 parms.toc←1↓1+⍳6
 parms.numberHeaders←2 3 4 5
 parms.verbose←0

 ns←##.MarkAPL.Init parms md
 ns←##.MarkAPL.Process ns

 toc←'<nav.*>.*</nav>'⎕R'&'⍠('Greedy' 0)('DotAll' 1)('Mode' 'M')⊣ns.html
 →T.PassesIf 3=+/∊'<ul>'∘⍷¨toc
 →T.PassesIf 3=+/∊'</ul>'∘⍷¨toc
 tags←{⍵↑⍨⍵⍳'>'}¨toc
 expected←'<nav id="main_nav_no_collapse"' '<ul' '<li' '<ul' '<li' '<ul' '<li' '<li' '</ul' '</ul'
 expected,←'<li' '<li' '</ul' '</nav'
 →T.PassesIf∧/expected∊tags

 R←T._OK
⍝Done
