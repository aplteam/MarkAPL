 R←Test_EmbeddedParms_21(stopFlag batchFlag);⎕TRAP;md;b;ns;html;hits;filenames;path;parms
⍝ Check that a parameter definition tolerates a single empty line
 ⎕TRAP←(999 'C' '. ⍝ Deliberate error')(0 'N')
 R←∆Failed

 md←''
 md,←⊂'[parm]:cssURL      = ''Firlefanz'''
 md,←⊂''
 md,←⊂'[parm]:linkToCSS   = 1'
 md,←⊂''
 md,←⊂''
 md,←⊂'A paragraph'

 parms←##.MarkAPL.CreateParms
 parms.verbose←0
 ns←##.MarkAPL.Init parms md
 ns←##.MarkAPL.Process ns

 →PassesIf 0=≢ns.report
 →PassesIf 2=≢ns.embeddedParms
 →PassesIf 1=+/'Firlefanz'⍷ns.parms.cssURL
 →PassesIf ns.parms.linkToCSS=1
 →PassesIf 0=+/'<p>[parm]:linkToCSS = 1</p>'⍷∊ns.html

 R←∆OK
