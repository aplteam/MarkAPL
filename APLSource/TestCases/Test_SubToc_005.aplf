 R←Test_SubToc_005(stopFlag batchFlag);⎕TRAP;md;ns;parms
⍝ Test <<SubTOC>> (not numbered) when there are two headers to be reported one of which has special attributes assigned.
 ⎕TRAP←(999 'C' '. ⍝ Deliberate error')(0 'N')
 R←∆Failed

 md←''
 md,←⊂'# Level 1{.center}'
 md,←⊂'<<SubTOC>>'
 md,←⊂'## Level 2'
 md,←⊂'This is a para'
 md,←⊂'## Level 3'
 md,←⊂'This is another para'
 md,←⊂''

 parms←##.MarkAPL.CreateParms
 parms.numberHeaders←1
 parms.toc←1
 ns←##.MarkAPL.Init parms md
 ns←##.MarkAPL.Process ns

 →PassesIf 1=+/'<h1 '⍷∊ns.html
 →PassesIf 1=+/'<h3>'⍷∊ns.html
 →PassesIf 2=+/'<h2>'⍷∊ns.html
 →PassesIf 3=+/'<p>'⍷∊ns.html   ⍝ Caption of sub-toc is a para as well!
 →PassesIf 0=+/'<<SubTOC>>'⍷∊ns.html
 →PassesIf 2=⍴'<nav.*>'⎕S 0⍠('Greedy' 0)⊣ns.html
 →PassesIf 1=+/'<h1 class="center">1. Level 1</h1>'⍷∊ns.html

 R←∆OK
