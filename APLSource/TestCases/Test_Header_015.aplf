 R←Test_Header_015(stopFlag batchFlag);⎕TRAP;md;ns;parms
⍝ Test TOC with header anchors having " in the header. Was once a bug.
 ⎕TRAP←(999 'C' '. ⍝ Deliberate error')(0 'N')
 R←T._Failed

 md←''
 md,←⊂'The "=" and "-" syntax (SeText)'
 md,←⊂'==============================='
 md,←⊂''
 md,←⊂'The "+" and "-" list marker{#foo style="color:red;"}'
 md,←⊂'-----------------------------'
 md,←⊂''

 parms←MarkAPL.CreateParms
 parms.numberHeaders←6                  ⍝ Number ALL headers
 parms.verbose←0
 parms.toc←6

 ns←MarkAPL.Init parms md
 ns←MarkAPL.Process ns

 →T.PassesIf(⊂'<a href="#the--and---syntax" id="the--and---syntax" class="autoheader_anchor">')∊ns.html
 →T.PassesIf'<h1[^>]*>1. The “=” and “-” syntax \(SeText\)</h1>'∆FoundIn ns.html
 →T.PassesIf(⊂'<li class="toc-entry toc-h1"><a href="#the--and---syntax">1. The “=” and “-” syntax (SeText)</a>')∊ns.html
 →T.PassesIf(⊂'<li class="toc-entry toc-h2"><a href="#foo">1.1. The “+” and “-” list marker</a>')∊ns.html
 →T.PassesIf(⊂'<a href="#foo" id="foo" class="autoheader_anchor">')∊ns.html
 →T.PassesIf(⊂'<h2 data-id="foo" style="color:red;">1.1. The “+” and “-” list marker</h2>')∊ns.html

 R←T._OK
⍝Done
