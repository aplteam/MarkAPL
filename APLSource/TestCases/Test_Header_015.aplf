 R←Test_Header_015(stopFlag batchFlag);⎕TRAP;md;ns;parms
⍝ Test TOC with header anchors having " in the header. Was once a bug.
 ⎕TRAP←(999 'C' '. ⍝ Deliberate error')(0 'N')
 R←∆Failed

 md←''
 md,←⊂'The "=" and "-" syntax (SeText)'
 md,←⊂'==============================='
 md,←⊂''
 md,←⊂'The "+" and "-" list marker{#foo style="color:red;"}'
 md,←⊂'-----------------------------'
 md,←⊂''

 parms←##.MarkAPL.CreateParms
 parms.numberHeaders←6                  ⍝ Number ALL headers
 parms.verbose←0
 parms.toc←6

 ns←##.MarkAPL.Init parms md
 ns←##.MarkAPL.Process ns

 →PassesIf(⊂'<a href="#The--and---syntax" id="The--and---syntax" class="autoheader_anchor">')∊ns.html
 →PassesIf(⊂'<h1>1. The “=” and “-” syntax (SeText)</h1>')∊ns.html
 →PassesIf(⊂'<li><a href="#The--and---syntax">1. The “=” and “-” syntax (SeText)</a>')∊ns.html
 →PassesIf(⊂'<li><a href="#foo">1.1. The “+” and “-” list marker</a>')∊ns.html
 →PassesIf(⊂'<a href="#foo" id="foo" class="autoheader_anchor">')∊ns.html
 →PassesIf(⊂'<h2 style="color:red;">1.1. The “+” and “-” list marker</h2>')∊ns.html

 R←∆OK
