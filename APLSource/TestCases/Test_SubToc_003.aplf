 R←Test_SubToc_003(stopFlag batchFlag);⎕TRAP;md;ns;parms
⍝ Test <<SubTOC>> (not numbered) when there are two headers to be reported.
 ⎕TRAP←(999 'C' '. ⍝ Deliberate error')(0 'N')
 R←T._Failed

 md←''
 md,←⊂'# Level 1'
 md,←⊂'<<SubTOC>>'
 md,←⊂'## Level 2'
 md,←⊂'This is a para'
 md,←⊂'### Level 3'
 md,←⊂'This is another para'
 md,←⊂''

 parms←MarkAPL.CreateParms
 parms.toc←4
 ns←MarkAPL.Init parms md
 ns←MarkAPL.Process ns

 →T.PassesIf 1=+/'<h2 data-id="Level-2">'⍷∊ns.html
 →T.PassesIf 1=+/'<h3 data-id="Level-3">'⍷∊ns.html
 →T.PassesIf 3=+/'<p>'⍷∊ns.html   ⍝ Caption of sub-toc is a para as well!
 →T.PassesIf 0=+/'<<SubTOC>>'⍷∊ns.html
 →T.PassesIf 2=⍴'<nav.*>'⎕S 0⍠('Greedy' 0)⊣ns.html

 R←T._OK
