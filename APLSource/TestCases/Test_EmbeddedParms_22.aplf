 R←Test_EmbeddedParms_22(stopFlag batchFlag);⎕TRAP;md;b;ns;html;hits;filenames;path;parms
⍝ Check that a parameter definition tolerates a comment line surrounded by empty lines
 ⎕TRAP←(999 'C' '. ⍝ Deliberate error')(0 'N')
 R←T._Failed

 md←''
 md,←⊂'[parm]:cssURL      = ''Firlefanz'''
 md,←⊂''
 md,←⊂'⍝'
 md,←⊂''
 md,←⊂'[parm]:linkToCSS   = 1'
 md,←⊂''
 md,←⊂''
 md,←⊂'A paragraph'

 parms←MarkAPL.CreateParms
 parms.verbose←0
 ns←MarkAPL.Init parms md
 ns←MarkAPL.Process ns

 →T.PassesIf 0=≢ns.report
 →T.PassesIf 2=≢ns.embeddedParms
 →T.PassesIf 1=+/'Firlefanz'⍷ns.parms.cssURL
 →T.PassesIf ns.parms.linkToCSS=1
 →T.PassesIf 0=+/'<p>[parm]:linkToCSS = 1</p>'⍷∊ns.html

 R←T._OK
