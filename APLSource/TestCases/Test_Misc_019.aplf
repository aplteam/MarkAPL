 R←Test_Misc_019(stopFlag batchFlag);⎕TRAP;path;filename;md;parms;report
⍝ Exercise `ConvertMarkdownFile` with `inputFilename` specified: must crash.
 ⎕TRAP←(999 'C' '. ⍝ Deliberate error')(0 'N')
 R←T._Failed

 path←FilesAndDirs.GetTempPath,'\MarkAPL_Tests\',⊃⎕SI
 :If FilesAndDirs.IsDir path
     FilesAndDirs.RmDir path
 :EndIf
 FilesAndDirs.MkDir path
 filename←path,'\Test_Misc_018.md'
 md←'# Test' '' '* This' '* That' '* More'
 (⊂md)⎕NPUT filename 1
 parms←##.MarkAPL.CreateParms
 parms.inputFilename←'Whatever'

 :Trap 11
     report←parms ##.MarkAPL.ConvertMarkdownFile filename
     →T.GoToTidyUp 1
 :Else
     →T.GoToTidyUp'"inputFilename" must not be specified'≢⊃⎕DM
     R←T._OK
 :EndTrap

∆TidyUp:
 FilesAndDirs.RmDir path
