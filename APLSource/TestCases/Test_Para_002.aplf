 R←Test_Para_002(stopFlag batchFlag);⎕TRAP;ns;m;expected
⍝ Test what belongs to a paragraph.
 ⎕TRAP←(999 'C' '. ⍝ Deliberate error')(0 'N')
 R←1                                                                   ⍝ Not OK

 m←'This is a paragraph' '* list'                                    ⍝ bulleted list
 ns←MarkAPL.Init''m
 ns←MarkAPL.Process ns
 →T.FailsIf'<p>This is a paragraph</p>' '<ul>' '<li>list</li>' '</ul>'≢ns.html

 m←'This is a paragraph' '917. list'                                 ⍝ Ordered list
 ns←MarkAPL.Init''m
 ns←MarkAPL.Process ns
 →T.FailsIf'<p>This is a paragraph</p>' '<ol start="917">' '<li>list</li>' '</ol>'≢ns.html

 m←'This is a paragraph' '> BlockQuote'                                ⍝ Blockquote
 ns←MarkAPL.Init''m
 ns←MarkAPL.Process ns
 →T.FailsIf'<p>This is a paragraph</p>' '<blockquote>' '<p>BlockQuote</p>' '</blockquote>'≢ns.html

 m←'This is a paragraph' '<pre>' 'APL Code' '</pre>'                   ⍝ Embedded HTML
 ns←MarkAPL.Init''m
 ns←MarkAPL.Process ns
 →T.FailsIf'<p>This is a paragraph</p>' '<pre><code>APL Code</code></pre>'≢ns.html

 m←'This is a paragraph' '# caption'                                   ⍝ Header line (pound syntax)
 ns←MarkAPL.Init''m
 ns←MarkAPL.Process ns
 expected←⊂'<p>This is a paragraph</p>'
 expected,←⊂'<div class="h_tag">'
 expected,←⊂'<a href="#caption" id="caption" class="autoheader_anchor">'
 expected,←⊂'<h1 data-id="caption">caption</h1>'
 expected,←'</a>' '</div>'
 →T.FailsIf expected≢ns.html

 m←'This is a paragraph' 'caption' '==='                               ⍝ Header line (= syntax)
 ns←MarkAPL.Init''m
 ns←MarkAPL.Process ns
 expected←⊂'<div class="h_tag">'
 expected,←⊂'<a href="#this-is-a-paragraph-caption" id="this-is-a-paragraph-caption" class="autoheader_anchor">'
 expected,←⊂'<h1 data-id="This-is-a-paragraph-caption">This is a paragraph caption</h1>'
 expected,←'</a>' '</div>'
 →T.FailsIf~∧/expected≡ns.html

 m←'This is a paragraph' 'caption' '---'                               ⍝ Header line (- syntax)
 ns←MarkAPL.Init''m
 ns←MarkAPL.Process ns
 expected←⊂'<a href="#this-is-a-paragraph-caption" id="this-is-a-paragraph-caption" class="autoheader_anchor">'
 expected,←⊂'<h2 data-id="This-is-a-paragraph-caption">This is a paragraph caption</h2>'
 →T.FailsIf~∧/expected∊ns.html

 m←'This is a paragraph' '_ _ _ _ '                                    ⍝ Horizontal ruler
 ns←MarkAPL.Init''m
 ns←MarkAPL.Process ns
 →T.FailsIf'<p>This is a paragraph</p>' '<hr>'≢2↑ns.html

 m←'This is a paragraph' '|col1|col2|'                                 ⍝ Table
 ns←MarkAPL.Init''m
 ns←MarkAPL.Process ns
 →T.FailsIf'<p>This is a paragraph</p>' '<table>'≢2↑ns.html

 m←'This is a paragraph' '```' 'APL code' '```'                        ⍝ Code block (```)
 ns←MarkAPL.Init''m
 ns←MarkAPL.Process ns
 →T.FailsIf'<p>This is a paragraph</p>' '<pre tabindex="0"><code>APL code</code></pre>'≢2↑ns.html

 m←'This is a paragraph' '~~~' 'APL code' '~~~'                        ⍝ Code block (~~~)
 ns←MarkAPL.Init''m
 ns←MarkAPL.Process ns
 →T.FailsIf'<p>This is a paragraph</p>' '<pre tabindex="0"><code>APL code</code></pre>'≢2↑ns.html

 m←'This is a paragraph' '~~~ {.classname}' 'APL code' '~~~'           ⍝ Code block (~~~) with special attribute
 ns←MarkAPL.Init''m
 ns←MarkAPL.Process ns
 expected←'<p>This is a paragraph</p>' '<pre tabindex="0"><code class="classname">APL code</code></pre>'
 →T.FailsIf expected≢ns.html

 m←'This and **_that' '3.1 and more_**' '' 'Foo'
 ns←MarkAPL.Init''m
 ns←MarkAPL.Process ns
 expected←'<p>This and <strong><em>that 3.1 and more</em></strong></p>' '<p>Foo</p>'
 →T.FailsIf expected≢2↑ns.html
 R←0                                                                   ⍝ OK
