 R←Test_LeanPub_031(stopFlag batchFlag);⎕TRAP;ns;md;parms;expected;html;home
⍝ Test the leanpub extension "highlight lines of code" with a non-default CSS file.
 ⎕TRAP←(999 'C' '. ⍝ Deliberate error')(0 'N')
 R←T._Failed

 md←''
 md,←⊂'A para'
 md,←⊂'~~~'
 md,←⊂'(+⌿⍵)÷⍴,⍵'
 md,←⊂'leanpub-start-insert'
 md,←⊂'{{⍵/⍨2=+⌿0=⍵∘.|⍵}⍳⍵}'
 md,←⊂'leanpub-end-insert'
 md,←⊂'(+⌿⍵)÷⍴,⍵'
 md,←⊂'~~~'
 md,←⊂'Another para'

 parms←MarkAPL.CreateParms
 parms.leanpubExtensions←1
 parms.createFullHtmlPage←1
 parms.linkToCSS←1
 parms.screenCSS←'BlackOnWhite_screen.css'
 parms.printCSS←'BlackOnWhite_print.css'

 ns←MarkAPL.Init parms md
 ns←MarkAPL.Process ns
 html←parms MarkAPL.MakeHTML_Doc ns.html

 home←FilesAndDirs.EnforceSlash FilesAndDirs.PWD,'/'
 expected←''
 expected,←⊂'<link href="file://',(FilesAndDirs.EnforceSlash parms.homeFolder,'/Files/BlackOnWhite_screen.css"'),' rel="stylesheet" media="screen">'
 expected,←⊂'<link href="file://',(FilesAndDirs.EnforceSlash parms.homeFolder,'/Files/BlackOnWhite_print.css"'),' rel="stylesheet" media="print">'
 →T.PassesIf∧/expected∊html

 R←T._OK
