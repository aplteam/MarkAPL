 r←parms MakeHTML_Doc html;bool;lines;sh;ind;copyCodeJavaScript;toggleTocJavaScript;javaScript;buff;hasTOC;hasCodeBlock;hasCollapsible
⍝ Takes HTML, typically created by calling `Process`, and makes it a fully fledged document by adding
⍝ <body>, <head> -- with <title> -- and <html> including the DocType. By default CSS is injected as well.
 :Access Public Shared
 r←⊂'<!DOCTYPE html>'
 r,←⊂'<html',((0≠≢parms.lang)/' lang="',(parms.lang~'"'),'"'),(parms.smoothScrolling/' style="scroll-behavior:smooth" '),'>'
 r,←⊂'<head>'
 :If parms.enforceEdge
          ⍝ ↓↓↓ https://blogs.msdn.microsoft.com/askie/2009/03/23/understanding-compatibility-modes-in-internet-explorer-8/
     r,←⊂'<meta http-equiv="X-UA-Compatible" content="IE=edge">'
          ⍝ This MUST be the first <meta> tag!
 :EndIf
 r,←⊂'<meta charset="',parms.charset,'">'
 :If ⎕NULL≡parms.title
     :If 1=+/ind←∨/¨⊃∨/'<h1 ' '<h1>'{⍺∘⍷¨⍵}¨⊂html
         parms.title←{⍵↑⍨¯1+⍵⍳'<'}{⍵↓⍨⍵⍳'>'}{⍵↓⍨¯1+1⍳⍨'<h1'⍷⍵}(ind⍳1)⊃html
     :Else
         parms.title←'MarkAPL'
     :EndIf
 :EndIf
 r,←⊂'<title>',parms.title,'</title>'
 parms←EstablishDefaultHomeFolder parms
 :If 0=parms.noCSS
     :If ⎕NULL≡parms.cssURL
         parms.cssURL←parms.homeFolder,'/Files/'
     :EndIf
     parms.cssURL~←'"'
     :If 0≠≢parms.cssURL
         parms.cssURL,←{'/'/⍨~(¯1↑⍵)∊'/\'}parms.cssURL
         ((parms.cssURL='\')/parms.cssURL)←'/'
     :EndIf
     :If parms.linkToCSS
         r,←'screen'InjectCssFilenamesIntoHtml parms
         :If 0≠≢parms.printCSS
             r,←'print'InjectCssFilenamesIntoHtml parms
         :EndIf
     :Else
         :If 0≠≢parms.screenCSS
             r,←'screen'InjectCssIntoHtml parms
         :EndIf
         :If 0≠≢parms.printCSS
             r,←'print'InjectCssIntoHtml parms
         :EndIf
     :EndIf
 :EndIf
 buff←∊html
 hasCodeBlock←∨/'<pre'⍷buff
 hasCollapsible←∨/'<summary'⍷buff
 hasTOC←∨/'<nav id="main_nav"'⍷buff
 buff←''
 :If hasCollapsible
 :AndIf parms.javaScript
     r,←'' '<script>'
     r,←GetJavaScriptForPrintingCollapsibles
     r,←'' '</script>'
 :EndIf
 :If 0≠≢parms.head
     r,←,⊆parms.head
 :EndIf
 copyCodeJavaScript←toggleTocJavaScript←''
 :If parms.javaScript
     :If hasCodeBlock
         copyCodeJavaScript←GetCopyCodeJavaScript parms.copy2ClipboardBtn
     :EndIf
     :If hasTOC
         toggleTocJavaScript←GetJavaScriptForTogglingTOC
     :EndIf
     javaScript←copyCodeJavaScript,toggleTocJavaScript,hasCollapsible/GetJavaScriptForTogglingCollapsibles
     :If 0<≢javaScript
         javaScript←('' '<script>'),javaScript,('</script>' '')
     :EndIf
 :Else
     javaScript←''
 :EndIf
 r,←'</head>' '<body>',html,javaScript,'</body>' '</html>'
⍝Done
