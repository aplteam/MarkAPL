 r←ProcessMarkAPLExtensions ns;noOfLines;b;list;html;details;b1;b2;ind;list2
⍝ Converts `!> ` and `=>` to <details> and <summary>
 r←0
 noOfLines←+/∧\~ns.emptyLines
 list←noOfLines↑ns.markdown
 (b1 b2)←{b1←'!> '∘≡¨⍵ ⋄ b2←'=> '∘≡¨⍵ ⋄ b1 b2}3↑¨list
 :If 1≤+/b2
     :If 1=+/b1
         ⍝ It's a single collapsible
         b←b1∨b2
         list2←3↓¨b/list
         html←⊂'<div class="collapsible">'
         html,←⊂'<details>'
         html,←⊂'<summary>'
         html,←1⍴ConvertH1AndH2HeadersToH3 1⍴list2
         html,←⊂'</summary>'
         html,←⊂'<div class="collapsible-content">'
         html,←⊂'<hr>'
         html,←1⊃ConvertMarkdown2HTML 1↓list2
         html,←⊂'</div>'
         html,←⊂'</details>'
         html,←⊂'</div>'
         ns.html,←html
     :Else
         ⍝ It's multiple collapsibles and therefore an accordian
         html←⊂'<div class="accordion">'
         :For ind :In ⍸b1
             b←1,∧\ind↓b2
             list2←3↓¨b/(ind-1)↓list
             html,←⊂'<div>'
             html,←⊂'<details>'
             html,←⊂'<summary>'
             html,←1⍴ConvertH1AndH2HeadersToH3 1⍴list2
             html,←⊂'</summary>'
             html,←⊂'<div class="accordion-content">'
             html,←⊂'<hr>'
             html,←1⊃ConvertMarkdown2HTML 1↓list2
             html,←⊂'</div>'
             html,←⊂'</details>'
             html,←⊂'</div>'
         :EndFor
         html,←⊂'</div>'
         ns.html,←html
     :EndIf
     ns.noOf←noOfLines
     ns←Drop ns
     ∆LastLineWasEmpty←1
     r←1
 :EndIf
⍝Done
