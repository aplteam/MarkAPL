 ReportLinks←{
⍝ Injects a list with all external references together with a remark.
⍝ The resulting table is assigned the class "print_only" for obvious reasons.
     ns←⍵
     1≠ns.parms.reportLinks:ns
     html←FlattenHTML ns.html
     hits←'<a[^>]+class="external_link".*</a>'⎕S 0 1⍠('Greedy' 0)('Mode' 'D')⊣html
     0=≢hits:ns
     anchors←(-≢'</a>')↓¨hits{⍺[2]↑⍺[1]⌽⍵}¨⊂html
     urls←'href="'∘{{⍵↑⍨¯1+⍵⍳'"'}⍵↓⍨(¯1+≢⍺)+1⍳⍨⍺⍷⍵}¨anchors
     b←(urls⍳urls)=⍳≢urls  ⍝ For dropping doubles
     (anchors urls)←b∘/¨anchors urls
     linkTexts←{⍵↓⍨⍵⍳'>'}¨anchors
     linkTexts←ns.parms{0 ProcessInlineMarkUp_ ⍵ ⍺}¨linkTexts
     md←CreateMarkdownFromUrlAndLinkText urls linkTexts
     md←'' '---' ''('**',ns.parms.reportLinksCaption,'**')'',md,''
     ns2←Init''md
     ns2←Process ns2
     html2←InjectLinkTextIntoReportLink ns2.html linkTexts
     ns.html,←(⊂'<div id="external_link_report" class="print_only">'),html2,(⊂'</div>')
     ns
 }
