 R←Test_BR_015(stopFlag batchFlag);⎕TRAP;md;ns;parms;html;buff
⍝ Test replacing `<<br>>` by a `<br/>` tag. Also heavy tests for recognizing lists.
 ⎕TRAP←(999 'C' '. ⍝ Deliberate error')(0 'N')
 R←∆Failed

 md←''
 md,←⊂'First para<<br>>with a line break in between'
 md,←⊂''
 md,←⊂'* Any list item<<br>>'
 md,←⊂'with a `<br/>` tag in it'
 md,←⊂'* Another one'
 md,←⊂''
 md,←⊂'1. First<<br>>'
 md,←⊂'ordered list item'
 md,←⊂''
 md,←⊂'   Second para'
 md,←⊂'of first item\'
 md,←⊂'with a `<br/>`'
 md,←⊂'1. Second list item'
 md,←⊂''
 md,←⊂'Col 1 | Col 2'
 md,←⊂'-|-'
 md,←⊂'First Cell | Second<<br>>cell'

 ns←##.MarkAPL.Init''md
 ns←##.MarkAPL.Process ns

 →PassesIf+/'<p>First para <br>with a line break in between</p>'⍷0⊃ns.html
 →PassesIf'<li>Any list item <br> with a <code>&lt;br/&gt;</code> tag in it</li>'≡2⊃ns.html
 →PassesIf'<li>Another one</li>'≡3⊃ns.html
 →PassesIf'<li>First <br> ordered list item'≡6⊃ns.html
 →PassesIf'<p>Second para of first item <br> with a <code>&lt;br/&gt;</code></p></li>'≡7⊃ns.html
 →PassesIf'<li>Second list item</li>'≡8⊃ns.html
 html←1↓↑,/(⎕UCS 13),¨ns.html
 →PassesIf 2=+/'<li>'⍷{⍵/⍨{⍵∨≠\⍵}↑∨/'<ul>' '</ul>'⍷¨⊂⍵}html  ⍝ First (bulleted) list has two items
 →PassesIf 2=+/'<li>'⍷{⍵/⍨{⍵∨≠\⍵}↑∨/'<ol' '</ol>'⍷¨⊂⍵}html  ⍝ Second (ordered) list has two items

 R←∆OK
