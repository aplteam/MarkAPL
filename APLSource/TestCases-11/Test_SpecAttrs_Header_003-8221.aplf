 R←Test_SpecAttrs_Header_003(stopFlag batchFlag);⎕TRAP;md;ns;parms
⍝ Headers: several assignments for SeText headers for level one and two with `bookmarkLink←2`.
 ⎕TRAP←(999 'C' '. ⍝ Deliberate error')(0 'N')
 R←∆Failed

 md←''
 md,←⊂'Title {#foo1 .header1}'
 md,←⊂'======'
 md,←⊂''
 md,←⊂'Sub-Title {.foo2}'
 md,←⊂'--------'
 md,←⊂''
 md,←⊂'### Chapter 1 {attr="123" #goo}'
 md,←⊂''
 md,←⊂'A paragraph'

 parms←##.MarkAPL.CreateParms
 parms.bookmarkLink←2
 ns←##.MarkAPL.Init parms md
 ns←##.MarkAPL.Process ns

 →PassesIf(⊂'<a href="#foo1" id="foo1" class="autoheader_anchor">')∊ns.html
 →PassesIf(⊂'<h1 class="header1">Title</h1>')∊ns.html
 →PassesIf(⊂'<a href="#Sub-Title" id="Sub-Title" class="autoheader_anchor">')∊ns.html
 →PassesIf(⊂'<h2 class="foo2">Sub-Title</h2>')∊ns.html
 →PassesIf(⊂'<h3 id="goo" attr="123">Chapter 1</h3>')∊ns.html

 R←∆OK
