 R←Test_ZZZ_999(stopFlag batchFlag);v;n;d;xml;rf;f1;f2;f4;buffer;is;shouldBe;⎕TRAP
⍝ Check the "Version" function and publish.config.
 ⎕TRAP←(999 'C' '. ⍝ Deliberate error')(0 'N')
 R←∆Failed

 rf←##.MarkAPL

⍝ First we check whether "Version" returns a valid result:
 (n v d)←rf.Version
 f1←n≢##.APLTreeUtils.Last⍕rf
 f1∨←~5≤⍴v
 f1∨←2≠'.'+.=v
 f1∨←~∧/⎕D∊⍨v~'.'

 f1∨←10≠⍴d
 f1∨←d[4 7]∨.≠'-'
 f1∨←~∧/⎕D∊⍨d~'-'

⍝ publish.config must be in line with what "Version" returns of course:
 xml←⎕XML'flat'#.APLTreeUtils.ReadUtf8File'publish.config'
 f2←v≢2⊃xml[xml[;1]⍳⊂'version';]
 f2∨←d≢2⊃xml[xml[;1]⍳⊂'date';]

 :If f1
     ⎕←'Result of the "Version" function is invalid.'
 :EndIf
 :If f2
     ⎕←'The contents of "publish.config" is not in line with "Version".'
 :EndIf

 →FailsIf f1∨f2

 R←∆OK
